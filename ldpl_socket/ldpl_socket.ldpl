extension "ldpl_socket.cpp"

data:
    LDPL_SOCKET_MSG is external text
    LDPL_SOCKET_IP is external text
    LDPL_SOCKET_PORT is external number
    LDPL_SOCKET_NUMBER is external number 

    ldpl.socket.msg is text
    ldpl.socket.ip is text
    ldpl.socket.port is number
    ldpl.socket.number is number 
procedure:

sub ldpl.socket.connect
    parameters:
        ip is text
        port is number
        socket_number is number
    procedure:
        store ip in LDPL_SOCKET_IP
        store port in LDPL_SOCKET_PORT
        call external LDPL_SOCKET_CONNECT
        store LDPL_SOCKET_NUMBER in socket_number 
end sub
create statement "SOCKET CONNECT TO $ AT $ IN $" executing ldpl.socket.connect

sub ldpl.socket.close
    parameters:
        socket_dest is number
    procedure:
        store socket_dest in LDPL_SOCKET_NUMBER
        call external LDPL_SOCKET_CLOSE
end sub
create statement "SOCKET CLOSE $" executing ldpl.socket.close

sub ldpl.socket.read
    parameters:
        sock is number
        msg_var is text
    procedure:
        store sock in LDPL_SOCKET_NUMBER
        call external LDPL_SOCKET_READ
        store LDPL_SOCKET_MSG in msg_var
end sub
create statement "SOCKET READ FROM $ in $" executing ldpl.socket.read

sub ldpl.socket.poll
    parameters:
        sock is number
        msg_var is text
    procedure:
        store sock in LDPL_SOCKET_NUMBER
        call external LDPL_SOCKET_POLL
        store LDPL_SOCKET_MSG in msg_var
end sub
create statement "SOCKET POLL FROM $ in $" executing ldpl.socket.poll

sub ldpl.socket.send
    parameters:
        message is text
        socket_dest is number
    procedure:
        store message in LDPL_SOCKET_MSG
        store socket_dest in LDPL_SOCKET_NUMBER
        call external LDPL_SOCKET_SENDMESSAGE
end sub
create statement "SOCKET SEND $ TO $" executing ldpl.socket.send
